
namespace SuperMarioBros;

public class LoginScene : ALittle.DisplayLayout
{
	private ALittle.Text _top_score;
	private ALittle.Text _startgame_text;
	private ALittle.Text _editworld_text;
	private ALittle.Image _select_cursor;

	private ALittle.Text _score;
	private ALittle.Text _coin;

    private int _option = 1;

    public fun Show()
    {
        this.visible = true;
        A_UISystem.keydown_callback = bind(this.HandleKeyDown, this);

        g_GCenter.player_data.life = 3;

        this._top_score.text = g_GConfig.GetInt("top_score", 0);
        this._score.text = g_GCenter.player_data.score;
        this._coin.text = 0;
    }

    private fun HandleKeyDown(int mod, int sym, int scancode)
    {
        if (sym == ALittle.UIEnumTypes.KEY_G)
        {
            this._option += 1;
            if (this._option > 2) this._option = 1;

            if (this._option == 1)
            	this._select_cursor.y = this._startgame_text.y + this._startgame_text.height / 2 - this._select_cursor.height / 2;
            elseif (this._option == 2)
            	this._select_cursor.y = this._editworld_text.y + this._editworld_text.height / 2 - this._select_cursor.height / 2;
        }

        if (sym == ALittle.UIEnumTypes.KEY_H)
        {
            if (this._option == 2)
            {
                A_UISystem.keydown_callback = null;
                this.Hide();
                g_GCenter.edit_scene.Show();
            }
            elseif (this._option == 1)
            {
                A_UISystem.keydown_callback = null;
                this.Hide();
                g_GCenter.stage_scene.Show(1, 1);
            }
        }
    }

    public fun UpdateTopScore()
    {
        var top_score = g_GConfig.GetInt("top_score", 0);
        if (top_score < g_GCenter.player_data.score)
        {
        	top_score = g_GCenter.player_data.score;
            g_GConfig.SetConfig("top_score", top_score);
            this._score.text = g_GCenter.player_data.score;
        }
    }

    public fun Hide()
    {
        this.visible = false;
    }
}
